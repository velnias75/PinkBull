import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'java'

archivesBaseName = 'PinkBull'
version = '1.1'

repositories {
	mavenLocal()

	maven {
		name = 'Spigot'
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
		
		content {
			includeGroup 'org.bukkit'
			includeGroup 'org.spigotmc'
	      }
	}
	
	maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
	maven { url = 'https://oss.sonatype.org/content/repositories/central' }
	
	mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(16))
    }
}

dependencies {

	compileOnly "org.spigotmc:spigot-api:${project.spigot_version}"
	
	implementation "org.bstats:bstats-bukkit:${project.bstats_version}"
	shadow "org.bstats:bstats-bukkit:${project.bstats_version}"
	
	implementation "dev.derklaro.spiget:http-java11:${project.spiget_version}"
	shadow "dev.derklaro.spiget:http-java11:${project.spiget_version}"
	
	implementation "dev.derklaro.spiget:mapper-gson:${project.spiget_version}"
	shadow "dev.derklaro.spiget:mapper-gson:${project.spiget_version}"
	
	compileOnly "com.google.code.findbugs:annotations:${project.findbugs_version}"
	annotationProcessor "com.google.code.findbugs:annotations:${project.findbugs_version}"
	
	testImplementation "org.spigotmc:spigot-api:${project.spigot_version}"
	testImplementation "junit:junit:${project.junit_version}"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
	duplicatesStrategy = 'include'
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {

	archiveClassifier.set('')    
    relocate 'org.bstats', 'de.rangun.pinkbull.shadowed.org.bstats'
    relocate 'dev.derklaro.spiget', 'de.rangun.RainManNG.shadowed.dev.derklaro.spiget'

    //Exclude META-INF entries of shaded deps
    exclude 'META-INF/**'
    exclude '**/*.pdn'
    exclude '**/*.bbmodel'
    from('.') {
        include 'LICENSE.md'
		include 'NOTICE.txt'
		include 'licenses/**'
    }
    minimize()
}

assemble {
	dependsOn shadowJar
}

test {
    testLogging {
      events "failed"

      showExceptions true
      exceptionFormat "full"
      showCauses true
      showStackTraces true

      showStandardStreams = false
    }
  }
